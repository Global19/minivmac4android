apply plugin: 'com.android.application'

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file('release.properties')))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        versionCode 43

        minSdkVersion 14
        targetSdkVersion 27
    }

    signingConfigs {
        release {
            storeFile file(props.keyStore)
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'abi', 'machine'
    productFlavors {
        macPlus {
            dimension 'machine'
            applicationId 'name.osher.gil.minivmac'
            versionName '1.5.0'

            ndk {
                moduleName 'mnvmcoreplus'
            }
        }

        macII {
            dimension 'machine'
            applicationId 'ninja.gil.miniv.ii'
            versionName '2.5.0'

            ndk {
                moduleName 'mnvmcoreii'
            }
        }

        x86 {
            dimension 'abi'
            ndk {
                abiFilter 'x86'
            }
        }
        x86_64 {
            dimension 'abi'
            ndk {
                abiFilter 'x86_64'
            }
        }
        armv7 {
            dimension 'abi'
            ndk {
                abiFilter 'armeabi-v7a'
            }
        }
        arm64 {
            dimension 'abi'
            ndk {
                abiFilter 'arm64-v8a'
            }
        }
        fat {
            dimension 'abi'
            ndk {
                abiFilters 'x86', 'x86_64', 'arm64-v8a', 'armeabi-v7a'
            }
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/Android.mk'
        }
    }
}

dependencies {
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    implementation 'com.nononsenseapps:filepicker:4.1.0'
}
